%Soundness of complete development wrt parallel reduction

rec sound : (g:pcctx)[g |- cd E E'] -> [g |- pred E E'] =
/ total d (sound _ _ _ d) /
fn d => case d of
| [g |- #p.4] => [g |- #p.2]
| [g |- cd_beta (\x.\u.\v.D1[..,x,u,v]) D2] =>
          let [g, b:block(x:term, w: pred x x, u: notlam x, v: cd x x)|- IH[..,b.1,b.2]] =
          sound [g, b:block(x:term, w: pred x x, u: notlam x, v: cd x x) |- D1[..,b.1,b.3,b.4]] in
          let [g |- IH2] = sound [g |- D2] in
          [g |- beta (\x.\u.IH[..,x,u]) IH2]
| [g |- cd_lm \x.\u.\v.D[..,x,u,v]] =>
          let [g, b:block(x:term, w: pred x x, u: notlam x, v: cd x x)|- IH[..,b.1,b.2]] =
          sound [g, b:block(x:term, w: pred x x, u: notlam x, v: cd x x) |- D[..,b.1,b.3,b.4]] in
          [g |- lm \x.\v.IH[..,x,v]]
| [g |- cd_ap D' D1 D2] =>
          let [g |- IH1] = sound [g |- D1] in
          let [g |- IH2] = sound [g |- D2] in
          [g |- ap IH1 IH2]
;
