
% Cex to CR(beta) untyped

%%% Untyped lambda-calculus

term : type.  %name term M.

lam : (term -> term) -> term.
app : term -> term -> term.

--> : term -> term -> type.  %infix none 10 -->.
			     %name --> R.

beta1 :   (app (lam M1) M2) --> M1 M2.

lm1   :       ({x:term} M x --> M' x)
      ->            (lam M) --> (lam M').

apl1  :              M1 --> M1'
      ->    (app M1 M2) --> (app M1' M2).

apr1  :              M2 --> M2'
      ->    (app M1 M2) --> (app M1 M2').

% Multi-step reduction

-->* : term -> term -> type.  %infix none 10 -->*.
			      %name -->* R*.

id1   :    M -->* M.

% step1 :    M' --> M'' ->  M  -->* M' -> M  -->* M''.

step1 : M --> M' -> M' -->* M'' -> M -->* M''.


% incomplete implementation of not equal

diff : term -> term -> type.

dal : diff  (lam  _) (app _ _).
dla : diff  (app _ _) (lam  _) .
dac1 : diff (app E1 E2) (app F1 F2)
	<- diff E1 F1.
dac2 : diff (app E1 E2) (app F1 F2)
	<- diff E2 F2.
dl2 : diff (lam  M) (lam N)
       <- {x:term} ({L: term -> term} diff x (lam L) -> diff (lam L ) x) ->
	  ({A1:term}{A2:term} diff x (app A1 A2) -> diff (app A1 A2 ) x) 
	  ->	 diff (M x) (N x). % sound but incomplete


id = lam [x] x.
delta = lam [x] (app x x).
cex = (app delta (app id id)).

%query 2 2  P:  cex --> M.

eq : term -> term -> type.

refl : eq M M.
joinable : term -> term -> type.

j1 : joinable M M.
jd: joinable M1 M2
     <-  M1 --> P1
     <-  M2 --> P2
     <- eq P1 P2.
     
not_joinable : term -> term -> type.

nj: not_joinable M1 M2
     <- diff M1 M2
     <-  M1 --> P1
     <-  M2 --> P2
     <- diff P1 P2.

cexcr : term -> term -> type.

c : cexcr M1 M2
     <-  cex --> M1
     <-  cex --> M2
     <- not_joinable M1 M2.

nat : type.
z : nat.
sc : nat -> nat.

1 = sc z.
2 = sc 1.
3 = sc 2.
4 = sc 3.
5 = sc 4.

size : nat -> nat -> term -> type.
hei : nat -> term -> type.


h3: hei (sc H) (app M N)
     <- hei H M
     <- hei H N.
h4: hei (sc H) (lam M)
     <- ({x:term} ({h:nat} hei h x) -> hei H (M x)).

gencex : nat -> term -> term -> term -> type.

c : gencex I M M1 M2
     <- hei I M
     <-  M --> M1
     <-  M --> M2
     <- not_joinable M1 M2.

%query 1 1 gencex 3 M M1 M2.

